buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.0.0-beta6'
	}
}
apply plugin: 'com.android.application'

dependencies {
	compile 'com.android.support:support-v4:23.1.1'
	compile 'com.android.support:appcompat-v7:23.1.1'
	compile 'com.google.android.gms:play-services:8.4.0'
}



android {
	compileSdkVersion 23
	buildToolsVersion '21.1.2'

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}

		instrumentTest.setRoot('tests')
	}

	lintOptions {
		abortOnError false
	}

	signingConfigs {
		release
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.txt'
		}
	}
}

// specify signing properties on the command line
if (hasProperty('storeFile')) {
	println 'Generating a signed package.'
	android.signingConfigs.release.storeFile = file(storeFile)
	android.signingConfigs.release.storePassword = storePassword
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPassword
} else {
	android.buildTypes.release.signingConfig = null
}
