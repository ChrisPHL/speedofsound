buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.12.2'
	}
}
apply plugin: 'com.android.application'

dependencies {
	compile 'com.android.support:support-v4:20.0.0'
	compile 'com.android.support:appcompat-v7:20.0.0'
	compile 'com.google.android.gms:play-services:6.1.11'
}



android {
	compileSdkVersion 19
	buildToolsVersion '20.0.0'

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}

		instrumentTest.setRoot('tests')
	}

	lintOptions {
		abortOnError false
	}

	signingConfigs {
		release
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			runProguard true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-googleplay.txt', 'proguard.txt'
			// https://android-review.googlesource.com/#/c/109101
			// shrinkResources true
		}
	}
}

// specify signing properties on the command line
if (hasProperty('storeFile')) {
	println 'Generating a signed package.'
	android.signingConfigs.release.storeFile = file(storeFile)
	android.signingConfigs.release.storePassword = storePassword
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPassword
} else {
	android.buildTypes.release.signingConfig = null
}
